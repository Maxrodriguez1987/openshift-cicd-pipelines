//@Library("pipeline-library") 
//import com.redhat.openshift.pipelines.Commons

def lib = library identifier: 'pipeline-library@master', retriever: modernSCM(
  [$class: 'GitSCMSource',
   remote: 'https://github.com/redhatcsargentina/openshift-pipelines.git',
   credentialsId: 'incubator-git-secret'])

def commons = new lib.com.redhat.openshift.pipelines.Commons(this)

pipeline {
    agent {
        label env.JENKINS_AGENT
    }
    options {
        skipDefaultCheckout()
        disableConcurrentBuilds()
    }
    stages {
        stage("Initialize") {
            steps {
                script {
                    // If tag environment variable is not set, uses latest
                    if (!env.TAG)
                        env.TAG = "latest"
                    
                    // Unless set, automatically tags the image with environment variable tag
                    if (!env.MANUAL_TAG)
                        env.MANUAL_TAG = "false"

                    // Gets the application project
                    env.PROJECT = commons.getApplicationProject()                         
                }
            }
        }
        // If Git branch environment variable is not set, asks for it
        stage("Set Branch") {
            when {
                not {
                    expression {
                        return env.GIT_BRANCH
                    }
                }         
            }
            steps {
                script {
                    env.GIT_BRANCH = input(id: 'branch',
                                           message: 'Select branch',
                                           parameters: [string(name: 'Branch')]);
                }
            }
        }
        stage("Checkout Code") {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(env.PROJECT) {
                            commons.gitCheckout(env.GIT_REPO, env.GIT_BRANCH, env.GIT_SECRET)
                        }
                    }
                }
            }
        }
        stage ("Apply Template") {
            when {
                expression {
                    return env.APP_TEMPLATE_FILE
                }
            }
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(env.PROJECT) {
                            // Uses the template to create the application
                            openshift.apply(openshift.process(readFile(file: env.APP_TEMPLATE_FILE), "-p", "PARAM_APP=${env.APP}"));
                        }
                    }
                }
            }
        }
        stage("Compile Code") {
            when {
                not {
                    expression {
                        return env.COMPILE_COMMAND.equals("none")
                    }
                }  
            }
            steps {
                sh env.COMPILE_COMMAND
            }
        }
        stage("Test Code") {
            when {
                not {
                    expression {
                        return env.TEST_COMMAND.equals("none")
                    }
                } 
            }
            steps {
                sh env.TEST_COMMAND
            }
        }
        stage("Analize Code") {
            when {
                not {
                    expression {
                        return env.CODE_ANALYSIS_COMMAND.equals("none")
                    }
                } 
            }
            steps {
                sh env.CODE_ANALYSIS_COMMAND
            }
        }
        stage("Release Artifacts") {
            when {
                not {
                    expression {
                        return env.RELEASE_COMMAND.equals("none")
                    }
                }        
            }
            steps {
                sh env.RELEASE_COMMAND
            }
        }
        stage("Build Image") {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(env.PROJECT) {
                            commons.buildImage(env.APP, env.ARTIFACTS_DIR)
                        }
                    }
                }
            }
        }
        stage("Tag Image") {
            when {
                expression {
                    return env.TAG_VERSION
                }   
            }
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(env.PROJECT) {
                            openshift.tag("${env.APP}:latest", "${env.APP}:${commons.getTag(env.TECH)}");
                        }
                    }
                } 
            }
        }
        stage("Deploy Application") {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(env.PROJECT) {
                            commons.deployApplication(env.APP, env.TAG)
                        } 
                    }
                }
            }
        }
    }
}